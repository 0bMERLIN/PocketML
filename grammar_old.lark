
// ============ EXPRESSIONS
?start: expr
?expr: atom | expr atom -> app

?atom0: atom0 "." NAME -> access
    | "(" tupleelems ")" -> _tuple
    | "do" "{" _sep{expr, ";"}  (";")?"}" -> do
    | none_expr -> none
    | "import" _sep{NAME, "."} ";" expr -> _import
    | "module" "(" ("*" | _sep{export, ","}) ")" -> module // no empty modules allowed!
    | "case" expr "|" _sep{case, "|"} -> match
    | "if" expr "then" expr "else" expr -> ite
    | NAME -> var
    | "let" NAME "=" expr ";" expr -> let
    | "let" NAME ":" type ";" expr -> letdecl
    | "data" NAME NAME* "=" constructors ";" expr -> typedef
    | "data" NAME NAME* ";" expr -> typedecl
    | "type" NAME NAME* "=" type ";" expr -> typealias
    | "let" "rec" NAME "=" expr ";" expr -> letrec
    | "(" expr ")"
    | NUMBER -> num
    | "-" atom -> neg
    | "{" entry ("," entry)* "}" -> record
    | (STRING | /"""(.|[\n])+"""/) -> string
    | /%%(.|[\n])+%%/ ";" expr -> python
    | /%(.|[\n])+%/ -> inlinepython

tupleelems: expr "," (expr ("," expr)*)?

?atom: "\\" NAME "->" expr -> lam
    | "\\" "case" lamcases -> lamcase
    | atom0
lamcases: _sep{case, "|"}
none_expr: "(" ")"

// modules
?export: "type" NAME -> typeexport
    | NAME -> valueexport

// pattern matching
?case: pattern "->" expr

?pattern: pattern patternatom -> papp
    | UPPERNAME -> pconstrname
    | patternatom
?patternatom: "_" -> pwildcard
    | LOWERNAME -> pvar
    | "(" pattern ")"
    | NUMBER -> pnum
    | STRING -> pstr
    | UPPERNAME -> pconstrname


// records
entry: NAME "=" expr

constructors: _sep{constructor, "|"}
constructor: UPPERNAME typeatom*


// ========= TYPES
?type: UPPERNAME typeatom* -> ttyp
    | typeatom
    | typeatom "->" type -> tfun

?typeatom: LOWERNAME -> tvar
    | "(" tupletelems ")" -> ttuple
    | "(" type ")"
    | UPPERNAME -> ttyp
    | "{" tentry ("," tentry)* "}" -> trecord

tupletelems: type "," (type ("," type)*)?

tentry: NAME ":" type

// ============== HELPERS

_sep{x, sep}: x (sep x)*

UPPERNAME: /[A-Z]/(NAME?)
LOWERNAME: /[a-z]/(NAME?)

// sml style comments (* blabla *)
COMMENT: /#.*/

%ignore COMMENT
%ignore WS
%import common.ESCAPED_STRING -> STRING
%import common.CNAME -> NAME
%import common.NUMBER
%import common.WS
