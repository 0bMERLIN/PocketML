
// ============ EXPRESSIONS
?start: expr
?expr: op0

%%%OPERATOR_TABLE%%%

?atom: "\\" NAME+ "->" expr -> lam
    | "\\" "case" lamcases -> lamcase
    | atom0
    | atom atom -> app

?atom0: atom0 "." NAME -> access
    | "(" tupleelems ")" -> _tuple
    | "|" tupleelems "|" -> nparray
    | "[" listelems "]" -> list
    | "do" "{" _sep{expr, ";"}  (";")?"}" -> do
    | none_expr -> none
    | "import" _sep{NAME, "."} ";" expr -> _import
    | "module" "(" ("*" | _sep{export, ","}) ")" -> module // no empty modules allowed!
    | "case" expr "|" _sep{case, "|"} -> match
    | "if" expr "then" expr ("else" expr)? -> ite
    | NAME -> var
    | "let" "rec" NAME NAME* "=" expr ";" expr -> letrec
    | "let" NAME NAME* "=" expr ";" expr -> let
    | "let" NAME ":" type ";" expr -> letdecl
    | "data" NAME NAME* "=" constructors ";" expr -> typedef
    | "data" NAME NAME* ";" expr -> typedecl
    | "type" NAME NAME* "=" type ";" expr -> typealias
    | "cache" ";" expr -> cache // deprecated
    | "(" expr ")"
    | NUMBER -> num
    | "{" entry ("," entry)* "}" -> record
    | (STRING | /"""(.|[\n])+"""/) -> string
    | /%%%(.|[\n])+%%%/ ";" expr -> python
    | /%%(.|[\n])+%%/ -> inlinepython

tupleelems: expr "," (expr ("," expr)*)?
listelems: expr ("," expr)* |

lamcases: _sep{case, "|"}
none_expr: "(" ")"

// modules
?export: "type" NAME -> typeexport
    | NAME -> valueexport

// pattern matching
?case: pattern "->" expr

?pattern: pattern patternatom -> papp
    | UPPERNAME -> pconstrname
    | patternatom
?patternatom: "_" -> pwildcard
    | LOWERNAME -> pvar
    | "(" pattern ")"
    | NUMBER -> pnum
    | STRING -> pstr
    | UPPERNAME -> pconstrname


// records
entry: NAME "=" expr

constructors: _sep{constructor, "|"}
constructor: UPPERNAME typeatom*


// ========= TYPES
?type: typeatom1
    | typeatom1 "->" type -> tfun

?typeatom1: typeatom
    | UPPERNAME typeatom* -> ttyp

?typeatom: LOWERNAME -> tvar
    | "(" tupletelems ")" -> ttuple
    | "(" type ")"
    | UPPERNAME -> ttyp
    | "{" tentry ("," tentry)* "}" -> trecord

tupletelems: type "," (type ("," type)*)?

tentry: NAME ":" type

// ============== HELPERS

_sep{x, sep}: x (sep x)*

UPPERNAME: /[A-Z]/(NAME?)
LOWERNAME: /[a-z]/(NAME?)

// sml style comments (* blabla *)
COMMENT: /#.*/

%ignore COMMENT
%ignore WS
%import common.ESCAPED_STRING -> STRING
%import common.CNAME -> NAME
%import common.NUMBER
%import common.WS
